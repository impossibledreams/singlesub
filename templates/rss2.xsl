<?xml version="1.0"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method = "xml" encoding="utf-8" indent="yes"/>	

	<xsl:param name="version"/>

<!--
#
#   SingleSub v0.1.1
#   Written by Yakov Shafranovich
#
#   A Project of SolidMatrix Research
#   Website: http://singlesub.sourceforge.net
#   Email:  research@solidmatrix.com
#   
#   Copyright (C) 2005 SolidMatrix Technologies, Inc.
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
#
-->

	<xsl:template match="/">
		<rss version="2.0">
<xsl:comment>
Generated by <xsl:value-of select="$version"/>.
SingleSub itself (not the output) is copyright (C) 2005 SolidMatrix Technologies, Inc.
SingleSub itself (not the output) is licensed under the Apache License, Version 2.0.
</xsl:comment>
			<channel>
			<title><xsl:value-of select="opml/head/title"/></title>
			<link>http://singlesub.sf.net</link>
			<copyright>Copyright (C) 2005 Yakov Shafranovich</copyright>
			<pubDate><xsl:value-of select="opml/head/dateCreated"/></pubDate>
			<lastBuildDate><xsl:value-of select="opml/head/dateModified"/></lastBuildDate>
			<description>This RSS feed contains a list of subscribe URLs for different RSS aggregators. It is automatically
				generated from the corresponding OPML file. Each item tag contains the subscribe url in the link tag,
				the aggregator name as title, category as category tag, html url as source tag and icon url as enclosure.
			</description>
			<docs>http://www.shaftek.org/code/singlesub/</docs>
      		<language>en-us</language>
			<generator><xsl:value-of select="$version"/></generator>
			<ttl>1440</ttl>
		
	 		 <xsl:apply-templates select="//outline[@type='subscribe']">
 			 	<xsl:sort select="@text"/>
 			 </xsl:apply-templates>
 			 
		</channel>
		</rss>
	</xsl:template>

	<xsl:template match="outline[@type='subscribe']">
		<item>
			<category><xsl:value-of select='../@text'/></category>
			<title><xsl:value-of select='@text'/></title>
			<description>Requires <xsl:choose>
				<xsl:when test="@escaped = 'true'">ESCAPED urls</xsl:when>
				<xsl:otherwise>UNESCAPED urls</xsl:otherwise>
				</xsl:choose>
			</description>
			<link><xsl:value-of select='@subscribeUrl'/></link>
			<source url="{@htmlUrl}"><xsl:value-of select='@htmlUrl'/></source>
			<xsl:if test="@iconUrl">
				<xsl:choose>
					<xsl:when test="substring(@iconUrl, string-length(@iconUrl)-3) = '.gif'">
						<enclosure url="{@iconUrl}" length="1024" type="image/gif"/>
					</xsl:when>
					<xsl:when test="substring(@iconUrl, string-length(@iconUrl)-3) = '.png'">
						<enclosure url="{@iconUrl}" length="1024" type="image/png"/>
					</xsl:when>
					<xsl:when test="substring(@iconUrl, string-length(@iconUrl)-3) = '.jpg'">
						<enclosure url="{@iconUrl}" length="1024" type="image/jpeg"/>
					</xsl:when>
				</xsl:choose>
			</xsl:if>
		</item>
	</xsl:template>

	<xsl:template match="text()">
	</xsl:template>


</xsl:stylesheet>